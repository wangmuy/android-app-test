apply plugin: 'com.android.application'

android {
    compileSdkVersion 24

    signingConfigs {
        def keystorePropertiesFile = rootProject.file("keystore.properties")
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        config {
            // https://developer.android.com/studio/publish/app-signing?hl=zh-cn#secure-key
            // 项目的根目录下创建一个名为 keystore.properties 的文件(svn/git 忽略), 包含以下内容
            /*
            PLATFORM_STORE_PASSWORD=myStorePassword
            PLATFORM_KEY_PASSWORD=mykeyPassword
            PLATFORM_KEY_ALIAS=myKeyAlias
            PLATFORM_STORE_FILE=E\:\\project\\aosp\\platform.keystore
            */
            storeFile file(keystoreProperties['PLATFORM_STORE_FILE'])
            storePassword keystoreProperties['PLATFORM_STORE_PASSWORD']
            keyAlias keystoreProperties['PLATFORM_KEY_ALIAS']
            keyPassword keystoreProperties['PLATFORM_KEY_PASSWORD']
        }
    }

    defaultConfig {
        applicationId "com.example.test"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.all {
        buildType -> buildType.signingConfig = signingConfigs.config
    }
}

dependencies {
    provided fileTree(dir: 'extlibs', include: ['*.jar'])
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.1'
}
